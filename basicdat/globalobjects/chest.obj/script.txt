//The States and Contents
//State 0: Closed chest
//State 1: Opened Chest
//State 2: Empty Chest
//Content 0: Locked chest
//Content 1: Unlocked Chest

// Someone cast an unlock spell
IfOrdered
  tmpx = selforder
  tmpy = 491  // The unique code for an unlock order
  IfXIsEqualToY
    // Is it closed?
    tmpargument = 0
    IfStateIs
      // Open it up
      tmpargument = ACTIONMG
      DoAction
        tmpargument = 1
        SetState
        tmpargument = 1
        SetContent
        tmpargument = 60
        SetBumpHeight
        KeepAction
        tmpargument = 0
        PlaySound
        // Wait a while
        tmpargument = 40
        SetTime
        // Tell the players...
        tmpargument = 3
        SendMessageNear

// Someone is trying to open it
IfBumped
  // Can they open the chest?
  SetTargetToWhoeverBumped
  IfTargetCanOpenStuff
    IfFacingTarget
      // Is it closed?
      tmpargument = 0
      IfStateIs
      
        //LOCKED
        tmpargument = 0
        IfContentIs  
          tmpargument = [DISA]
          IfTargetHasSkillID          //They picked the lock
          
            //Unlock it
            tmpargument = 1
            SetContent
            PlaySound
            tmpargument = 6
            SendMessageNear
            
            //Award some xp
            tmpargument = 25
            tmpdistance = EXPROLEPLAY
            GiveExperienceToTarget
            
            //Animate
            tmpargument = ACTIONMG
            TargetDoAction
          Else          
            tmpargument = [KEYC]
            CostTargetItemID          //They used a key
            
              //Unlock it
              tmpargument = 1  
              SetContent
              PlaySound
              tmpargument = 5
              SendMessageNear
              
              //Award some xp
              tmpargument = 10
              tmpdistance = EXPSECRET
              GiveExperienceToTarget
      
              //Animate
              tmpargument = ACTIONMG
              TargetDoAction
            Else
              IfTimeOut           //Tell it's locked
                tmpargument = 1
                SendMessageNear
                tmpargument = 40
                SetTime
            
        //UNLOCKED
        Else 
          // Give some experience
          tmpargument = 5
          tmpdistance = EXPDARE
          GiveExperienceToTarget

          // Open it up
          tmpargument = ACTIONMG
          DoAction
            tmpargument = 1
            SetState
            tmpargument = 60
            SetBumpHeight
            KeepAction
            tmpargument = 0
            PlaySound
        
            // Wait a while
            tmpargument = 40
            SetTime
    
            // Tell the players...
            tmpargument = 4
            SendMessageNear
      Else                
        // Wait before allowing loot
        IfTimeOut
          // Reset the timer
          tmpargument = 40
          SetTime
            
          // Make target crouch
          IfFacingTarget
            tmpargument = ACTIONMG
            TargetDoAction

          // Say it's empty...
          IfStateIs2
            // Say the chest is empty...
            tmpargument = 0
            SendMessageNear

          // Drop the contents
          IfStateIs1
            DropItems
            tmpargument = selfmoney
            DropMoney
            DropKeys
            tmpargument = 2
            SendMessageNear
            tmpargument = 2     //Empty now...
            SetState

// All done
End
