// ZZ> This function makes an item fall to the floor when spawned
IfSpawned				//
  tmpargument = ACTIONJB		  //
  DoAction				  //
  KeepAction				  //
  MakeCrushValid
IfCrushed
  tmpx = selfx
  tmpy = selfy
  tmpdistance = selfz
  tmpargument = 1
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  GoPoof
  tmpargument = 1
  SendMessageNear
IfDropped				// Make it lie on floor
  KeepAction				  //
IfGrabbed				// Tell them what they've won...
  SetTargetToWhoeverIsHolding		  //
  IfTargetIsAPlayer			  //
    tmpargument = 0			  //
    SendMessageNear			  //
IfTakenOut				// Do the unsheathe sound
  tmpargument = 11
  PlaySound
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 2
    SendMessageNear
IfHitGround				// Make a sound
  tmpargument = 10
  PlaySound

// Whisper when held
IfHeld
  IfTimeOut
    tmpargument = rand % 10
    PlaySound
    tmpargument = rand % 90
    SetTime

// White embers and smoke
tmpx = rand & 3
tmpy = 0
IfXIsEqualToY
  tmpargument = 4
  tmpdistance = rand % 9
  SpawnParticle
  tmpargument = 2
  tmpdistance = rand % 9
  SpawnParticle

//Damage bonuses vs. demons
IfHolderScoredAHit		//Sets target to holder last hit
  tmpargument = [DEMO]
  IfTargetHasID
    tmpargument = rand & 2 + 12
    PlaySound
    tmpargument = rand & 1024 + 512  //2-6 extra
    tmpargument = 0 - tmpargument
    GiveLifeToTarget
    tmpargument = [LORD]		//Even more vs. the Daemon Lord
    IfTargetHasID
      tmpargument = 0 - 1325		//5 true damage
      GiveLifeToTarget
    tmpx = rand & 3
    tmpy = 0
    IfXIsEqualToY
      tmpargument = 3
    tmpy = 1
    IfXIsEqualToY
      tmpargument = 4
    tmpy = 2
    IfXIsEqualToY
      tmpargument = 5
    tmpy = 3
    IfXIsEqualToY
      tmpargument = 6
    SendMessageNear

    tmpx = rand & 1
    tmpy = 1
    IfXIsEqualToY
      tmpargument = 13
    Else
      tmpargument = 14
    PlaySound
    tmpargument = 6
    tmpx = targetx
    tmpy = targety
    tmpdistance = targetdistance
    SpawnExactParticle
    SpawnExactParticle
    SpawnExactParticle
    SetTargetToSelf
    IfNameIsKnown
      DoNothing
    Else
      MakeNameKnown       // Now that you know what it does, you learn its name.

End					// All done
