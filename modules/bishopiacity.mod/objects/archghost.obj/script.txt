//Set correct messaging
IfSpawned
  tmpargument = 3
  SetContent

//Make the door appear visible
SetTargetToNearestLifeform
  IfTargetCanSeeInvisible
    tmpx = selfspawnx
    tmpy = selfspawny + 128
    tmpargument = 160         //- 30 = 130 is the number to revert
    SetTileXY

//Talk!
IfBumped
  SetTargetToWhoeverBumped
  IfTargetIsAPlayer
    IfTargetCanSeeInvisible
      tmpargument = [WMAG]
      IfTargetHasSkillID
        IfStateIs0

          //Wait before talking again
          tmpargument = 1
          SetState
          tmpargument = 150
          SetTime

          //They want to enter!
          tmpargument = 10
          IfContentIs
            tmpargument = 1
            SendMessage
            tmpargument = 7
            PlayFullSound
            tmpargument = [AMAG]
            AddQuest
            tmpargument = 5         //+1 later = 6
            SetContent
          Else
            GetContent
            PlaySound
            SendMessage

            //Find out if they want to enter
            tmpargument = 5
            IfContentIs
              tmpargument = 9       //+1 later = 10
              SetContent

            //Message 6 and 7 is actually one long message
            tmpargument = 6
            IfContentIs
              tmpargument = 5       //+1 later = 6
              SetContent
              tmpargument = 7
              SendMessage
              tmpargument = 230
              SetTime               //Longer message = longer timer...
          GetContent
          tmpargument = tmpargument + 1
          SetContent


      //The bumper is not a wizard!
      Else
        IfStateIs0
          tmpargument = 0
          SendMessage
          tmpargument = 2
          PlaySound
          tmpargument = 1
          SetState
          tmpargument = 150
          SetTime
    Else
      IfStateIs0
        tmpargument = 2
        SendMessage
        tmpargument = 1
        PlaySound
        tmpargument = 1
        SetState
        tmpargument = 150
        SetTime

//Enable talking again
IfTimeOut
  tmpargument = 0
  SetState

//------------------------------------------------------------------------------
End
//------------------------------------------------------------------------------
