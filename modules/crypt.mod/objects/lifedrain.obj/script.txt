// Create the character
IfSpawned
  KeepAction
  tmpargument = 0
  SetContent


// Say they pour over a book...
IfTakenOut
  tmpargument = 0
  SetContent
  SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      tmpargument = 0
      SendMessageNear

IfTimeOut
  tmpargument = 500
  SetTime
  tmpargument = 0
  SetContent

// Allow it to be used
IfUsed
  tmpargument = 50
  SetReloadTime
  SetTargetToWhoeverIsHolding
    tmpx = targetmana
    tmpy = 255
    IfXIsMoreThanY                  //Need 1 mana to cast
    
      // Give experience...
      IfTargetIsAPlayer
        IfNameIsKnown
          DoNothing
        Else
          MakeUsageKnown
          MakeNameKnown
          tmpargument = 30
          tmpdistance = EXPSECRET
          GiveExperienceToTarget
      
      // Do the effect
      tmpargument = 256
      CostTargetMana
        tmpargument = [UNDE]
        tmpdistance = 18        //BLAHENEMIES | BLAHINVERTID
        SetTargetToWideBlahID                       //Found living enemy that is not undead
          tmpdistance = 1
        Else
          SetTargetToNearestFriend
            tmpdistance = 1
            IfTargetIsSelf
              DoNothing
            Else
              tmpx = targetdistance
              tmpy = 600
              IfXIsMoreThanY
                tmpdistance = 0
          Else
            tmpdistance = 0
                
        tmpturn = 0
        IfDistanceIsMoreThanTurn       //If proceeds then found a target 
          tmpx = targetx
          tmpy = targety
          tmpdistance = targetz
          tmpargument = 1
          SpawnExactParticle
          tmpargument = rand & 511 + 1024         //4-6 life
          DamageTarget                  //Damage opponent
          SetTargetToWhoeverIsHolding
          tmpargument = 256 //Heal 1 hp
          HealTarget                    //And heal caster
        Else
          tmpargument = 100
          SetReloadTime
          SetTargetToWhoeverIsHolding
          IfTargetIsAPlayer
            tmpargument = 1
            SendMessageNear
            PlaySound

// Return to spellbook, Do last!
IfDropped
  GetContent
  tmpx = tmpargument
  tmpy = 1
  IfXIsEqualToY
    PlaySound
  tmpargument = 0
  SetContent
  BecomeSpellbook
  DisaffirmCharacter
  tmpargument = ACTIONJB
  DoAction
  KeepAction
  
End
