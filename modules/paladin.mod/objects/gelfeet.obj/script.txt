//------------------------------------------------------------------------------
IfHealed
  // Damage is inverted to make the Gelfeet invincible...
  tmpargument = 0
  tmpdistance = rand & 2047 + 10000
  PlaySound

  // Drop the item it's carrying
  tmpargument = ACTIONHA
  DoAction


IfTimeOut
  tmpargument = 100
  SetTime
  SetTargetToRider
    IfTargetIsOnHatedTeam
      // Hurt the character
      tmpargument = 512
      DamageTarget


      // Corrode items
      tmpx = rand & 1
      tmpy = 0
      IfXIsEqualToY
        SetTargetToTargetLeftHand
          tmpargument = [RUST]
          IfTargetHasVulnerabilityID
            tmpargument = 0
            SendMessageNear
            PoofTarget
      Else
        SetTargetToTargetRightHand
          tmpargument = [RUST]
          IfTargetHasVulnerabilityID
            tmpargument = 0
            SendMessageNear
            PoofTarget
    Else
      // Corrode items
      tmpargument = [RUST]
      IfTargetHasVulnerabilityID
        tmpargument = 0
        SendMessageNear
        PoofTarget


IfBumped				// Eat the enemy
  SetTargetToWhoeverBumped
  tmpx = targetz
  tmpy = selfz + 30
  IfXIsLessThanY
    IfFacingTarget
      tmpargument = ACTIONME
      DoAction
      tmpargument = 0
      tmpdistance = rand & 2047 + 6000
      PlaySound
IfSpawned				// This is done once in a lifetime
  ClearWaypoints			  // Now go there
  tmpx = 1688				  // Waypoint 0
  tmpy = 7262				  //
  AddWaypoint				  //
  tmpx = 1246				  // Waypoint 1
  tmpy = 7231				  //
  AddWaypoint				  //
  tmpx = 1264				  // Waypoint 2
  tmpy = 6924				  //
  AddWaypoint				  //
  tmpx = 1692				  // Waypoint 3
  tmpy = 6942
  AddWaypoint
End					// Finished with this character
