// Create the character
IfSpawned
  KeepAction


// Remove the charge
IfTakenOut
  tmpargument = 0
  SetContent
  SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      tmpargument = 3
      SendMessageNear


// Allow it to be used
IfUsed
  SetTargetToWhoeverIsHolding
    tmpargument = [WMAG]
    IfTargetHasSkillID
      GetContent
      tmpx = tmpargument
      tmpy = 768
      IfXIsLessThanY
        tmpy = targetmanaflow
        IfXIsLessThanY
          tmpx = 7
          tmpargument = tmpx
          CostTargetMana
            GetContent
            tmpargument = tmpargument + tmpx
            SetContent
            tmpx = targetx
            tmpy = targety
            tmpdistance = targetz
            tmpargument = 0
            SpawnExactParticle
      
      // Put particles around the target (If we find one)
      tmpx = 0
      SetTargetToNearbyEnemy        //Prefer enemies before others...
        tmpx = 1
      Else
        SetTargetToNearestLifeform
          tmpx = 1
          
      tmpy = 1
      IfXIsEqualToY
        tmpx = targetx
        tmpy = targety
        tmpdistance = targetz
        tmpargument = 0
        SpawnExactParticle


// Allow it to be cast or fizzle
Else
  GetContent
  tmpx = tmpargument
  tmpy = 767
  IfXIsMoreThanY
    // Make the holder cast it
    SetTargetToWhoeverIsHolding
      SetOwnerToTarget
      tmpargument = ACTIONZA
      CorrectActionForHand
      TargetDoAction
      
    // Find a target
    SetTargetToNearestLifeform
      // Can it be enchanted?
      tmpargument = [XENC]       //DEBUG: Remove?
      IfTargetHasAnyID
        // Can't be enchanted...
        tmpargument = 4
        SendMessageNear
      Else
        UnkurseTarget
        DisenchantTarget
        tmpargument = 2
        SendMessageNear        

    Else
      // Couldn't find a target, target the other hand (or the caster if that also fails)
      SetTargetToWhoeverIsHolding
      IfHeldInLeftHand
        SetTargetToTargetRightHand
      Else
        SetTargetToTargetLeftHand
      UnkurseTarget
      DisenchantTarget
      tmpargument = 2
      SendMessageNear   
  
  // Play a sound
    tmpargument = 0
    PlaySound
    tmpargument = 20
    SetReloadTime
  
  Else
    tmpy = 0
    IfXIsMoreThanY
      // Didn't pump it enough
      tmpargument = 1
      SendMessageNear
  // Reset the charge counter
  tmpargument = 0
  SetContent


// Return to spellbook, Do last!
IfDropped
  tmpargument = 0
  SetContent
  BecomeSpellbook
  DisaffirmCharacter
  tmpargument = ACTIONJB
  DoAction
  KeepAction

End
