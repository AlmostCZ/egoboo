// ZZ> This function makes the character wanders around its enemy
IfSpawned
  MakeCrushValid
  SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      SetOwnerToTarget
      JoinTargetTeam
      MakeNameKnown
  IfNameIsKnown
    DoNothing
  Else
    tmpx = rand % 100   //1-100%
    tmpy = 10
    IfXIsLessThanY
      tmpargument = 3
      ChangeArmor
    Else
      tmpy = 20
      IfXIsLessThanY
        tmpargument = 2
        ChangeArmor
      Else
        tmpy = 40
        IfXIsLessThanY
          tmpargument = 1
          ChangeArmor 
IfGrabbed
  MakeCrushInvalid
  IfNameIsKnown
    SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      SetOldTarget
      SetTargetToOwner
      IfTargetIsOldTarget
        tmpargument = 5
      Else
        tmpargument = 6
      SendMessageNear
  Else
    SetTargetToWhoeverIsHolding
    IfTargetIsAPlayer
      SetOwnerToTarget
      JoinTargetTeam
      MakeNameKnown
      tmpargument = 3
      SendMessageNear
      tmpargument = 4
      SendMessageNear
  SetTargetToSelf
  SetTargetToTargetLeftHand
    DropItems
    tmpargument = 16
    SendMessageNear
IfDropped
  SetTargetToSelf
  StopTargetMovement
  UnkeepAction
IfUsed
  SetTargetToWhoeverIsHolding
  tmpargument = 25
  SetTargetReloadTime
  IfTargetIsAPlayer
    // =Cuddles=
    SetTargetToSelf
    tmpargument = 7
    SendMessageNear

    // Level
    tmpx = targetlevel
    tmpy = 2
    IfXIsMoreThanY
      tmpx = 2
    tmpargument = 8 + tmpx
    SendMessageNear

    // Stats
    tmpx = targetlife > 9
    tmpy = 2
    IfXIsMoreThanY
      tmpx = 3
    tmpargument = 11 + tmpx
    tmpx = selfstr > 8
    tmpy = selfwis > 8
    SendMessageNear

    // Stats
    tmpx = selfint > 8
    tmpy = selfdex > 8
    tmpdistance = targetexp
    tmpargument = 15
    SendMessageNear
IfCrushed
  tmpx = selfx
  tmpy = selfy
  tmpdistance = selfz
  tmpargument = 4
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  SpawnExactParticle
  GoPoof
  tmpargument = 2
  SendMessageNear
IfCleanedUp				// Respawn
  IfTargetIsAlive			  // Make sure killer is away
    tmpx = 1100				    //
    tmpy = targetdistance		    //
    IfXIsLessThanY			    //
      RespawnCharacter			      //
  Else					  // Killer is far away
    RespawnCharacter			    //
IfKilled				// This reduces the height of the char
  IfNameIsKnown
    SetOldTarget
    SetTargetToOwner
    IfTargetIsAlive
      tmpargument = 0
      SendMessageNear
    SetTargetToOldTarget
  Else
    tmpargument = 1
    SendMessageNear
  tmpargument = 30			  // Dead height
  SetBumpHeight				  //
  tmpargument = 5			  // Sound
  tmpdistance = rand & 2047 + 12000	  //
  PlaySound				  //
IfAttacked				// Don't take kindly to attackers
  SetTargetToWhoeverAttacked		  //
    tmpargument = 4			    // Sound
    tmpdistance = rand & 2047 + 9300	    //
    PlaySound				    //
IfBumped				// Scooch around
  tmpx = rand & 1023 + targetx - 512
  tmpy = rand & 1023 + targety - 512
  ClearWaypoints
  AddWaypoint
IfTimeOut				// This is done every so often
  GetContent
  tmpx = tmpargument
  tmpy = 0
  IfXIsMoreThanY
    tmpargument = tmpargument - 1
    SetContent
    tmpdistance = rand & 2047 + 10000
    tmpargument = 12
  Else
    tmpx = rand & 15
    tmpy = 7
    IfXIsMoreThanY
      tmpargument = tmpx > 1
      SetContent
    tmpargument = rand & 15 + 30
  SetTime

  IfHeld
    // Don't bother moving around if held
    GetState
  Else
    SetTargetToOwner
    IfTargetIsSelf
      tmpx = rand & 1023 - 512 + selfspawnx
      tmpy = rand & 1023 - 512 + selfspawny
    Else
      SetTargetToNearbyEnemy
        tmpx = targetdistance
        tmpy = 150
        IfXIsLessThanY
          IfFacingTarget
            // Attack the enemy...
            DropItems
            tmpargument = ACTIONUA
            DoAction

        tmpx = targetx
        tmpy = targety
        tmpturn = targetturnto
        tmpdistance = 200
        Compass
      Else  // Munch a corpse
        tmpargument = [HUMA]
        tmpdistance = 7  // Dead Friends and Enemies...
        SetTargetToNearestBlahID
          tmpy = 50
          tmpx = targetdistance
          IfXIsLessThanY
            PoofTarget                   // remove
              SetTargetToSelf
              tmpargument = 200
              tmpdistance = EXPROLEPLAY
              GiveExperienceToTarget     // give exp
              tmpargument = 5
              tmpx = targetx
              tmpy = targety
              tmpdistance = targetz
              SpawnExactParticle         // make mess
              SpawnExactParticle       
              SpawnExactParticle       
              SpawnExactParticle       
              tmpargument = 0            // burp
              tmpdistance = 1024
              PlaySound
              tmpargument = 1024         // Get 4 life back
              HealSelf                   
          tmpx = targetx
          tmpy = targety
        Else
          SetTargetToOwner
            tmpy = 100
            tmpx = targetdistance
            IfXIsLessThanY
              tmpx = rand & 1023 - 512 + selfx
              tmpy = rand & 1023 - 512 + selfy
            Else
              tmpx = ownerx
              tmpy = ownery

    ClearWaypoints			  //
    AddWaypoint				  //

End					// Finished with this character
