// Show the character's location
tmpx = selfx
tmpy = selfy
tmpargument = BLUE
ShowBlipXY

IfKilled				// This reduces the height of the char
  tmpargument = 6
  PlaySound

  tmpargument = 0			  // Last words...
  SetState				  //
  IfTargetIsOnSameTeam			  // Fragged!
    tmpargument = 3			    //
    IfTargetIsSelf			    // No, just a damage tile
      tmpargument = 4			      //
  SendMessage				  //
  tmpargument = selfmoney			  // Drop money
  DropMoney				  //
  tmpargument = 45			  //
  SetBumpHeight				  //

IfAttacked				// Attacked
  SetTargetToWhoeverAttacked		  //
    IfTargetIsAlive			    //
      IfTargetIsOnHatedTeam		      // Go after 'em
        tmpargument = 1			        //
        SetState			              // Ouch!
        tmpargument = tmpdistance & 1 + 4
        PlaySound			        //
      Else				      // Complain
        tmpargument = 2			        //
        SendMessageNear			        //
        tmpargument = 1			        //
        PlaySound			        //
        SetTargetToOldTarget		        //
    Else				    //Attacker died already
      SetTargetToOldTarget		      //

IfTargetKilled				// Return to follow mode
  tmpargument = 0			  // State 0 ( Follow )
  SetState				  //

IfTimeOut				// This is done every so often
  tmpargument = rand & 15 + 10			  // Try again later
  SetTime

  tmpx = selfcontent
  tmpy = 1
  IfXIsEqualToY
    // Say "After Her"
    tmpargument = 8
    PlayFullSound

    tmpargument = 5
    SendMessageNear

    tmpargument = 0
    SetContent
  Else
    IfXIsMoreThanY
      // Say it next time
      tmpargument = tmpx - 1
      SetContent
      
  IfStateIs0				  // State 0 ( Follow )
    tmpx = leaderdistance
    tmpy = 128*5
    IfXIsMoreThanY
      tmpargument = 133        //Catch up fast! 33% bonus speed
      SetSpeedPercent
    Else
      tmpargument = 100
      SetSpeedPercent
    tmpdistance = 100         //Walk normal
    tmpx = leaderx			    //
    tmpy = leadery			    //
    tmpturn = 16384 + leaderturn	    // Stand to side
    Compass				    //
    ClearWaypoints			  //
    AddWaypoint				  //

    //Look out for enemies
    SetTargetToNearbyEnemy		  
      tmpargument = 1			      //
      SetState				      // Change to combat mode
      
      //Cry out if it's the mimic!
      tmpargument = selfhateid		      //
      IfTargetHasID			      // Battle cry versus hate group
        IfTargetIsOldTarget		        //
          DoNothing
        Else				        //
          tmpargument = 1			          //
          SendMessageNear			          //
          tmpargument = 3			          //
          PlaySound			          //

  // State 1 ( Combat )
  IfStateIs1					  
    SetTargetToNearbyEnemy
      
      //Attack
      IfFacingTarget
        tmpx = targetdistance		    // Close enough to attack?
        tmpy = 200				    //
        IfXIsLessThanY			    //
          tmpargument = LATCHRIGHT		      // Right Attack == 2
          PressLatchButton			      //
      
      //Keep within 6 tiles distance of enemy or player
      tmpx = targetdistance
      tmpy = 128*6                
      IfXIsMoreThanY                
        tmpargument = 0
        SetState
        
      tmpx = leaderdistance
      IfXIsMoreThanY
        tmpx = targetdistance
        tmpy = 200            //Only retreat if not in close combat
        IfXIsMoreThanY
          tmpargument = 0
          SetState
      
      //Special movement in case its the jelly cube
      tmpargument = [CUBE]
      IfTargetHasID
        tmpargument = [HUMA]
        IfTargetHoldingItemID
          tmpdistance = 200         //Go save the humanoid who is trapped inside!
        Else
          tmpdistance = -300     //Keep away from the gelfeet
      Else
        tmpdistance = 200			//Else move in to attack
        
      // Move towards enemy (tmpdistance is calculated above)
      tmpx = targetx			    
      tmpy = targety			    //
      tmpturn = targetturnto		    //
      Compass				    //
      ClearWaypoints			  //
      AddWaypoint				  //

    //No enemies around
    Else
      tmpargument = 0
      SetState
      
// Use shield?
IfTargetIsAttacking
  IfTargetIsOnHatedTeam
    IfFacingTarget
      tmpargument = LATCHLEFT
      PressLatchButton

IfBumped				// Bumped
  SetTargetToWhoeverBumped
  IfTargetIsAlive
    IfTargetIsOnHatedTeam			  // Try to counter
      tmpargument = LATCHRIGHT		    //
      PressLatchButton			    //
    Else					  // Get around other character
      SetTargetToNearbyEnemy		    // Look out for enemies
        tmpargument = 1			      // Stay in combat state
      Else				    // Revert
        tmpargument = 0			    // Return to follow state
        SetTargetToOldTarget		      //
      SetState				    //
      tmpx = rand & 511 + selfx - 256		    //
      tmpy = rand & 511 + selfy - 256		    //
      ClearWaypoints			    //
      AddWaypoint				    //
      tmpargument = 40			    // Try again soon
      SetTime				    //
  
  //Jump over bodies
  Else
    tmpargument = LATCHJUMP
    PressLatchButton
    
// Ya!  Unarmed attack sound
IfUsed
  tmpargument = 7
  PlaySound
End					// Finished with this character
