// Show the character's location
tmpx = selfx
tmpy = selfy
tmpargument = YELLOW
ShowBlipXY

IfKilled				// This reduces the height of the char
  tmpargument = 1
  PlaySound

  tmpargument = 0			  // Last words...
  SetState				  //
  IfTargetIsOnSameTeam			  // Fragged!
    tmpargument = 3			    //
    IfTargetIsSelf			    // No, just a damage tile
      tmpargument = 4			      //
  SendMessage				  //
  tmpargument = selfmoney			  // Drop money
  DropMoney				  //
  tmpargument = 45			  //
  SetBumpHeight				  //
      
IfTimeOut				// This is done every so often
  tmpargument = rand & 15 + 15		  // Try again later
  SetTime				  //

  //Find someone to heal
  tmpargument = 2
  IfStateIsNot
    SetTargetToNearestFriend
    IfTargetIsHurt
      tmpx = targetdistance
      tmpy = 400
      IfXIsLessThanY
        tmpargument = 2
        SetState

  // State 2 ( Helper )
  IfStateIs2
    SetTargetToNearestFriend
      tmpx = targetdistance
      tmpy = 128*6
      IfXIsLessThanY
        IfTargetIsHurt			    // Heal wounded
          tmpx = targetdistance		      //
          tmpy = 300			      //
          IfXIsLessThanY			      // In Range?
            tmpx = 260			        //
            tmpy = selfmana			        //
            IfXIsLessThanY				// Enough to cast?
              IfFacingTarget
                tmpargument = LATCHLEFT	          // Left Attack == 1
                PressLatchButton		          //
            Else					        //Not enough mana, do something else
              tmpargument = 0
              SetState
        Else				    
          tmpargument = 0     // Switch to follow
          SetState				      //    

      //move towards target
      tmpx = targetx			    //
      tmpy = targety			    //
      ClearWaypoints			  //
      AddWaypoint				  //
    Else
      tmpargument = 0
      SetState
      
  // State 0 ( Follow )
  IfStateIs0				  
    //Find someone to bash
    SetTargetToWideEnemy
      tmpargument = 1
      SetState
    Else    
      //Follow the player
      tmpx = leaderdistance
      tmpy = 128*6
      IfXIsMoreThanY
        tmpargument = 133        //Catch up fast!
        SetSpeedPercent
      Else
        tmpargument = 100
        SetSpeedPercent
      tmpdistance = 100         //Walk normal
      tmpx = leaderx			    //
      tmpy = leadery			    //
      tmpturn = 49152 + leaderturn	    // Stand to side
      Compass
      ClearWaypoints			  //
      AddWaypoint				  //

  // State 1 ( Combat )
  IfStateIs1				  
    SetTargetToNearbyEnemy		    //
      tmpx = targetdistance		    // Close enough to attack?
      tmpy = 250				    //
      IfXIsLessThanY			    //
        IfFacingTarget
          IfHoldingMeleeWeapon
            PressLatchButton

      //Retreat if leader is running and not in combat
      tmpx = leaderdistance
      tmpy = 128*6
      IfXIsMoreThanY
        tmpx = targetdistance
        tmpy = 200
        IfXIsLessThanY
          tmpargument = 0
          SetState
      
      //Move towards enemy
      tmpargument = [CUBE]
      IfTargetHasID
        tmpdistance = -350     //Keep away from the gelfeet
      Else
        tmpdistance = -50			//Else move in to attack
      
      tmpx = targetx			    
      tmpy = targety			    
      tmpturn = targetturnto		    //
      Compass				    
      ClearWaypoints			  //
      AddWaypoint
    
    //No enemies around, return to wander
    Else
      tmpargument = 0
      SetState
      
IfAttacked				// Counter attack if not healing
  SetTargetToWhoeverAttacked		    //
    IfTargetIsAlive			      //
      IfTargetIsOnHatedTeam		        // Go get 'em
        tmpargument = 1		          //
        SetState			          //
        tmpargument = rand & 1 + 2
        PlaySound
      IfTargetIsOnSameTeam		        // Yell at 'em
        tmpargument = MESSAGEOUCH	          //
        SendMessageNear		          //
        SetTargetToOldTarget		          //
        tmpargument = 4
        PlaySound
    Else				      // Attacker dead already
      SetTargetToOldTarget		        //  

IfBumped				// Bumped
  SetTargetToWhoeverBumped
  IfTargetIsAlive
    // Heal 'em
    IfTargetIsOnSameTeam
      IfTargetIsHurt			  
        tmpargument = 2			      // State 2 ( Helper )
        SetState				      //
    
    //Bash 'em
    IfTargetIsOnHatedTeam
      IfFacingTarget
        IfHoldingMeleeWeapon
          PressLatchButton			      //
        
    SetTargetToOldTarget		    //
    tmpx = rand & 511 + selfx - 256		  //
    tmpy = rand & 511 + selfy - 256		  //
    ClearWaypoints			  //
    AddWaypoint				  //
    tmpargument = 40			  // Try again soon
    SetTime				  //
  
  //Jump over bodies
  Else
    tmpargument = LATCHJUMP
    PressLatchButton
    

IfTargetKilled				// Mode switch
  tmpargument = 0			  // Return to follow mode
  SetState				  //

// Heal sound and effect
IfUsed
  tmpargument = 0
  PlayFullSound
  tmpargument = 50
  SetReloadTime
  IfTargetIsOnSameTeam
    tmpx = targetx
    tmpy = targety
    tmpdistance = targetz
    tmpargument = 0
    SpawnExactParticle

    tmpargument = rand & 511 + 512
    HealTarget

End					// Finished with this character
