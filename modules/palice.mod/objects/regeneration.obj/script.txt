//This function makes an item fall to the floor when spawned
IfSpawned
  tmpargument = ACTIONJB
  DoAction
  KeepAction
  MakeCrushValid
IfDropped				// Make it lie on floor
  KeepAction
IfGrabbed				// Tell them what they've won, Johnny!
  SetTargetToWhoeverIsHolding
  IfTargetIsAPlayer
    tmpargument = 0
    SendMessageNear
IfHitGround
  tmpargument = 0
  PlaySound
  tmpargument = ACTIONDB
  DoAction
IfHeld			//Hide in hand
  tmpargument = 1
  SetState
Else
  tmpargument = 0
  SetState

//Spell stuff
IfUsed
  UndoEnchant
  UnsparkleIcon
  
  SetTargetToWhoeverIsHolding
  SetOwnerToTarget
  tmpargument = [HMAG]
  IfTargetHasSkillID				//Need divine spell skill
    GetContent
    tmpx = tmpargument
    tmpy = 512					//Don't overcharge
    IfXIsLessThanY
      tmpy = targetmanaflow			//Enough spell power?
      IfYIsMoreThanX
        tmpargument = 4				//Mana cost
        CostTargetMana
          GetContent				//Increase the charge
          tmpargument = tmpargument + 4		//Casting Time
          SetContent
          
    //Make the target glow
    SetTargetToNearestFriend
      DoNothing
    Else
      SetTargetToWhoeverIsHolding
    tmpx = targetx
    tmpy = targety
    tmpdistance = targetz
    tmpargument = 0
    SpawnExactParticle
Else
  GetContent
  tmpx = tmpargument
  tmpy = 511
  IfXIsMoreThanY		//Enough power to activate?
    tmpargument = 100		//Set cooldown time
    SetReloadTime
    
    tmpargument = [XENC]
    IfTargetHasAnyID
      tmpargument = 5
      SendMessageNear		//Tell what happened
    Else
      tmpargument = 3
      SendMessageNear		//Tell what happened
      tmpargument = 1
      PlaySound
      EnchantTarget
      
      //Mana cost and power depends on level
      SetTargetToWhoeverIsHolding
      tmpargument = targetlevel*32 + 256  //Cost depends on level
      tmpdistance = 0      // Give owner 0 life per second
      tmpx = 0               // Give target 0 mana per second
      tmpy = tmpargument*2   // Give target twice the mana cost life per second
      SetEnchantBoostValues
  Else				
    GetContent
    tmpx = tmpargument
    tmpy = 0
    IfXIsMoreThanY		//Only if there was a charge
      tmpargument = 1
      SendMessageNear		//Tell it didn't work
      tmpargument = 2
      PlaySound
  tmpargument = 0
  SetContent			//Reset counter
  
  //Award xp and stuff for daring to do it
  IfNameIsKnown
    DoNothing
  Else
    tmpargument = 20
    tmpdistance = EXPDARE
    GiveExperienceToTarget
    MakeNameKnown
    MakeUsageKnown

// Put the little mana ball on the
// character's hand
GetContent
tmpx = tmpargument
tmpy = 0
IfXIsMoreThanY
  tmpturn = tmpargument < 4 + 3000
  tmpdistance = SPAWNORIGIN
  tmpargument = 2
  SpawnAttachedSizedParticle

End					// All done
